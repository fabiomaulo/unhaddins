"Name"     = 'TestGrammar'
"Author"   = 'Fabio Maulo'
"Version"  = '1.0.0'
"About"    = 'Initial grammar to create the base structure'

"Start Symbol" = <Expression>

! ------------------------------------------------- Sets

{ID Head}      = {Letter} + [_]
{ID Tail}      = {Alphanumeric} + [_]
{String Chars} = {Printable} + {HT} - ["\]
{Hex Digit} = {Digit} + [abcdefABCDEF]

! ------------------------------------------------- Terminals

Comment Start		= '/*'
Comment End		= '*/'
Comment Line		= '--'

Identifier		= {ID Head}{ID Tail}*
Path			= {ID Head}{ID Tail}* ('.' {ID Head}{ID Tail}*)+
StringLiteral		= '"' ( {String Chars} | '\' {Printable} )* '"'
IntegerLiteral	= {Digit}+
FloatLiteral		= {Digit}+'.'{Digit}+
HexLiteral		= 0x{Hex Digit}+
ComparisonOperator	= '>' | '<' | '=' | '>=' | '<=' | '<>' | '!='
Parameter		= ':'{ID Head}{ID Tail}* | '?'

! ------------------------------------------------- Rules

<Expression>  ::= <Expression> ComparisonOperator  <MathAddExpression> 
               |  <MathAddExpression> 

<MathAddExpression> ::= <MathAddExpression> '+' <MathMultExpression>
               |  <MathAddExpression> '-' <MathMultExpression>
               |  <MathMultExpression>

<MathMultExpression>    ::= <MathMultExpression> '*' <MathNegateExpression> 
               |  <MathMultExpression> '/' <MathNegateExpression> 
               |  <MathNegateExpression> 

<MathNegateExpression>  ::= '-' <Value> |  <Value> 

<Value>       ::= Identifier | Path | StringLiteral | IntegerLiteral | FloatLiteral | HexLiteral | Parameter | '(' <Expression> ')'